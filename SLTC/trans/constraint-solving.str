/**
 * Implements constraint solving via Robinson unification.
 */
module constraint-solving

imports constraint-language
imports utils

rules

    /**
     * Occurs check. Succeeds if `x' is contained in `t' and
     * fails otherwise.
     *
     * @type (x, t) -> a
     */
    occurs : (x, t) -> <sometd(?x)> t

    /**
     * Robinson unification. For details see:
     *   ROBINSON, J.A. Computational logic: The unification computation.
     *   ROBINSON, J.A. A machine-oriented logicbased on the resolution principle
     *
     * @type List(Constraint) -> (List(Constraint), List(Var, a), List(Error))
     */
    unify : cs -> <unify'> (cs, [], [])
    
    /**
     * A variant of unify that returns the mgu if the constraint set was solvable
     * and that fails otherwise.
     *
     * @type List(Constraint) -> List(Var, a) 
     */
    unify-internal : cs -> <unify'; where(Third; equal(|[])) < Snd + fail> (cs, [], [])
    unify' : ([], mgu, err) -> ([], mgu, err)
    unify' : ([CFail__(error) | cs], mgu, err) -> <unify'> (cs, mgu, [error | err])
    unify' : ([CEq__(t, t, error) | cs], mgu, err) -> <unify'> (cs, mgu, err)
    unify' : ([CNeq__(t, s, error)| cs], mgu, err) ->
        <if and(or(<?Var__(_)> t, <?Var__(_)> s), <all(?CNeq__(_, _ ,_))> cs)
         then !(cs, mgu, [CFail__("Only inequalities left.") | err])
         else if <not(equal)> (t, s)
              then <unify'> (cs, mgu, err)
              else <unify'> (<conc> (cs, <singelton> CNeq__(t, s, error)), mgu, err)
              end
         end>
        where <not(equal)> (t, s)
    unify' : ([CEq__(t, s, error) | cs], mgu, err) ->
        <if and(and(<?Var__(_)> t, <?Var__(_)> s), <all(?CEq__(Var__(_), Var__(_), _))> cs)
        then !(cs, mgu, [CFail__("Only equalities with variables left.") | err])
        else fail
        end>
    unify' : ([CEq__(t, Var__(x), error) | cs], mgu, err) ->
         <unify'> (<conc> (cs, <singelton> CEq__(Var__(x), t, error)), mgu, err)
    unify' : ([CEq__(v@Var__(x), t, error) | cs], mgu, err) -> <if <occurs> (v, t)
                                     then <unify'> (cs, mgu, [error | err])
                                     else <unify'> (<replace(|cs)> (v, t), [(v, t)|mgu], err)
                                     end>
    unify' : ([CEq__(n#(ts), n#(ts'), error) | cs], mgu, err) -> <unify'> (<conc> (cs, cs'), mgu, err)
        where <equal>(<length> ts, <length> ts');
              cs' := <zip; map(makeCEq(|error))> (ts, ts')
    unify' : ([c | cs], mgu, err) -> <unify'> (cs, mgu, [<get-error> c | err])

    /**
     * Applies the given mgu to term `t'. The difference to `replace-all'
     * is, that the substitution is also applied to the mgu itself.
     *
     * @param t - term to which the mgu is applied
     * @type List(Var, a) -> ATerm
     */
    apply-mgu(|t) : [] -> t
    apply-mgu(|t) : [(x, s) | mgu] -> <apply-mgu(|t')> mgu'
        where t' := <replace(|x, s, t)>
        where mgu' := <map(replace(|x, s))> mgu