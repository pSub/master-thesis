module constraint-template-language

signature
    sorts Rule Lookup Judgment Neq
    constructors
       /** Base Language */
       Rule__ : judg-number * Option(String) * Pattern * Outputs * Constraints -> Rule
       Binding__ : List(List(var)) * List(call) * Rule -> Rule

       /** Extended Language */
       Fork__ : List(Rule) -> Rule

       Lookup__  : ctx * inputs -> Lookup
       Reset__ : ctx -> Reset
       Judgment__ : number * term * binding -> Judgment
       Neq__ : term * term -> Neq

rules  

    /**
     * Extracts the pattern from Rule, Binding or Fork
     *
     * @type Rule -> List(a)
     */
    get-pattern : Rule__(_, _, (pattern, _), _, _) -> pattern
    get-pattern : Binding__(_, _, rule) -> <get-pattern> rule
    get-pattern : Fork__([rule | _]) -> <get-pattern> rule

    /**
     * Extracts the context pattern from Rule, Binding or Fork
     *
     * @type Rule -> List(a)
     */
    get-ctx-pattern : Rule__(_, _, (_, ctx), _, _) -> ctx
    get-ctx-pattern : Binding__(_, _, rule) -> <get-ctx-pattern> rule
    get-ctx-pattern : Fork__([rule | _]) -> <get-ctx-pattern> rule

    /**
     * Extracts the judgment number from the conclusion.
     *
     * @type Rule -> Int
     */
    get-judgment-number : Rule__(judg-number, _, _, _, _) -> judg-number
    get-judgment-number : Binding__(_, _, rule) -> <get-judgment-number> rule
    get-judgment-number : Fork__([rule | _]) -> <get-judgment-number> rule

    /**
     * Transforms a triple into a judgment node
     *
     * @type (name, a, b) -> Judgment(name, a, b)
     */
    mkJudgment : (name, a, b) -> Judgment__(name, a, b)

    /**
     * Transforms a tuple into a lookup node
     *
     * @type (a, b) -> Lookup(a, b)
     */
    mkLookup : (a, b) -> Lookup__(a, b)

    /**
     * Transforms a tuple into a fork node
     *
     * @type List(Rule) -> Fork(List(Rule))
     */
    mkFork : xs -> Fork__(xs)