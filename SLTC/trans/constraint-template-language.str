/**
 * Defines the constructors of the templates and provides
 * auxiliary definitions.
 */
module constraint-template-language

imports utils

signature
    sorts Template Conclusion Premise Ctx
    constructors
       /** Base Language */
       Template__ : Option(String) * List((Premise, Dependency)) * Conclusion -> Template
       Conclusion__ : judg-number * Pattern * Outputs -> Conclusion
       Judgment__ : judg-number * term * binding * outputs * error -> Premise
       Lookup__  : ctx * inputs * outputs * error -> Premise
       Eq__ : term * term * error -> Premise
       Neq__ : term * term * error -> Premise
       Reset__ : ctx -> Ctx
       Ctx__ : ctx -> Ctx
       Binding__ : ctx * inputs * outputs -> Ctx

       /** Extended Language */
       Fork__ : List(Rule) -> Template

rules

    /**
     * Extracts the inputs from a premise.
     *
     * @type Premise -> Inputs
     */
    get-premise-inputs : Binding__(_, inputs, _) -> inputs
    get-premise-inputs : Judgment__(_, inputs, bindings, _, _) ->
        <conc> (inputs, <mapconcat(get-premise-inputs)> bindings)
    get-premise-inputs : Lookup__(_, inputs, _, _) -> inputs
    get-premise-inputs : Eq__(a, b, _) -> [a, b]
    get-premise-inputs : Neq__(a, b, _) -> [a, b]

    /**
     * Extracts the outputs from a premise.
     *
     * @type Premise -> Inputs
     */
    get-premise-outputs : Binding__(_, _, outputs) -> outputs
    get-premise-outputs : Judgment__(_, _, bindings, outputs, _) ->
        <conc> (outputs, <mapconcat(get-premise-outputs)> bindings)
    get-premise-outputs : Lookup__(_, _, outputs, _) -> outputs
    get-premise-outputs : Eq__(_, _, _) -> []
    get-premise-outputs : Neq__(_, _, _) -> []

    /**
     * Succeeds if the input is a premise and fails otherwise.
     *
     * @type Premise -> Premise
     */
    is-premise = ?Judgment__(_,_,_,_,_) <+ ?Lookup__(_,_,_,_) <+ ?Eq__(_,_,_) <+ ?Neq__(_,_,_)

    /**
     * Extracts the pattern of the conclusion from a template.
     *
     * @type Template -> List(a)
     */
    get-pattern : Conclusion__(_, (pattern, _), _) -> pattern
    get-pattern : Template__(_, _, rule) -> <get-pattern> rule
    get-pattern : Fork__([rule | _]) -> <get-pattern> rule

    /**
     * Extracts the context pattern of the conclusion from a template.
     *
     * @type Template -> List(Ctx)
     */
    get-ctx-pattern : Conclusion__(_, (_, ctx), _) -> ctx
    get-ctx-pattern : Template__(_, _, rule) -> <get-ctx-pattern> rule
    get-ctx-pattern : Fork__([rule | _]) -> <get-ctx-pattern> rule

    /**
     * Extracts the judgment number of the conclusion from a template.
     *
     * @type Template -> Int
     */
    get-judgment-number : Conclusion__(judg-number, _, _) -> judg-number
    get-judgment-number : Template__(_, _, rule) -> <get-judgment-number> rule
    get-judgment-number : Fork__([rule | _]) -> <get-judgment-number> rule

    /**
     * Extracts the name of the template.
     *
     * @type Template -> Option(String)
     */
    get-rule-name : Template__(name, _, rule) -> name
    
    /**
     * Extract the outputs of the conclusion from the template.
     *
     * @type Template -> Option(Outputs)
     */
    get-outputs : Conclusion__(_, _, outputs) -> Some(outputs)
    get-outputs : Template__(_, _, rule) -> <get-outputs> rule
    get-outputs : Fork__(_) -> None()
    
    /**
     * Transforms a list of templates into a fork node. Forks created
     * with this rule will not be nested, i.e. the templates of all forks
     * in the input are extracted from the fork and inserted in the new fork.
     *
     * @type List(Template) -> Fork(List(Template))
     */
    mkFork : ts -> Fork__(<map(try(\Fork__(ts') -> ts'\)); flatten-list> ts)

    /**
     * Transforms a tuple into a equality node.
     *
     * @param error - Error
     * @type (a, b) -> Eq(a, b)
     */
    makeEq(|error) : (a, b) -> Eq__(a, b, error)

    /**
     * Transforms a tuple into a inequality node.
     *
     * @param error - Error
     * @type (a, b) -> Neq(a, b)
     */
    makeNeq(|error) : (a, b) -> Neq__(a, b, error)

    /**
     * Extracts the context id from a context modification.
     *
     * @type Ctx -> Int
     */
    get-ctx-id = proj(Reset__|1) <+ proj(Ctx__|1) <+ proj(Binding__|1)

    /**
     * Succeeds if the node is a context modification and fails otherwise.
     *
     * @type Ctx -> Ctx
     */
    is-ctx-modification = ?Reset__(_) <+ ?Ctx__(_) <+ ?Binding__(_, _, _)