module generate-constraint-generator

imports constraint-language
imports utils
imports include/SLTC

signature
    sorts
    constructors
       Rule : Option(String) * Pattern * Outputs * Constraints -> Rule
       Binding : List(List(var)) * List(call) * Rule -> Rule

       Lookup  : ctx * inputs * outputs -> Lookup
       Judgment : term * binding -> Judgment
       Neq : term * term -> Neq

       Pattern : term
       Outputs : List(term)
       Output : term -> Output
       
       ConstraintGenerator : List(Rule)
       
rules
    
    // FIXME: The where clause of this rule duplicates code.
    make-constraint-generator :
        Module(ModuleDec(_),
               imports,
               Language(l),
               MetaVariables(meta-variables),
               Contexts(context-definitions),
               Judgments(judgments),
               Rules(typing-rules),
               conjectures)
        -> <innermost(base(|judgments, context-classes) <+
                      step(|judgments, context-definitions, context-classes))> typing-rules
        where
            context-names := <map(proj(ContextDefinition|1))> context-definitions;
            context-classes := <filter(\MetaVariableDefinition(name, _, Scope(scope)) ->
                                       <isect; not(equal(|[])); !name> (context-names, scope)\)>
                                       meta-variables
    
    /**
     * Extracts the pattern from Rule or Binding
     */
    get-pattern : Rule(_, pattern, _, _) -> pattern
    get-pattern : Binding(_, _, Rule(_, pattern, _, _)) -> pattern

    /**
     * Wraps the given term with the constructor Output
     */
    mkOutput : n -> Output(n)

    mkJudgment : (n, m) -> Judgment(n, m)
    mkLookup : (a, b, c) -> Lookup(a, b, c)

    /**
     *
     */
    eq-inputs = where(get-generic-name; equal(|"Eq")); get-arguments
    eq-outputs = where(get-generic-name; equal(|"Eq")); ![]
    neq-inputs = where(get-generic-name; equal(|"Neq")); get-arguments
    neq-outputs = where(get-generic-name; equal(|"Neq")); ![]


    /**
     * Extacts all consituents from the given term that are marked
     * as inputs in the declaration.
     *
     * @param declarations - either a list of context of judgment declarations
     *                       or something of equivalent structure
     */
    get-inputs(|declarations) = eq-inputs <+ neq-inputs <+ filter-hole-positions(?(_, "I") | declarations)

    /**
     * Extacts all consituents from the given term that are marked
     * as outputs in the declaration and wraps those with Output. The
     * wrapping is used for identifying output terms in step.
     *
     * @param declarations - either a list of context of judgment declarations
     *                       or something of equivalent structure
     */
    get-outputs(|declarations) = get-outputs'(|declarations); map(mkOutput)

    /**
     * Like `get-outputs` but does not wrap everything into Output constructors.
     *
     * @param declarations - either a list of context of judgment declarations
     *                       or something of equivalent structure
     */
    get-outputs'(|declarations) = eq-outputs <+ neq-outputs <+ filter-hole-positions(?(_, "O") | declarations)

    /**
     * Extracts all consituents from a given term that satisfy the given
     * condition `cond`.
     *
     * @param cond - an arbitrary strategy
     * @param declarations - either a list of context of judgment declarations
     *                       or something of equivalent structure
     */
    filter-hole-positions(cond|declarations) : node -> consituents
        where
            args := <get-arguments> node;
            positions := <get-hole-positions(|declarations); filter(cond); map(Fst)> node;
            consituents := <map(\n -> <index(|n)> args\)> positions

    /**
     * Fetches the declaration for the given node and provides a list of
     * holes together with their positions.
     *
     * @param declarations - either a list of context of judgment declarations
     *                       or something of equivalent structure
     */
    get-hole-positions(|declarations) : cons#(args) -> <enum'> holes
        where
            [constant, generic] := <string-tokenize(|<singelton> '-')> cons;
            declaration-number := <string-to-int; inc> generic;
            declaration := <index(|declaration-number)> declarations;
            holes := <collect-all(?Hole(_,_), conc); map(proj(Hole|2))> declaration

    /**
     * Rule that transforms axioms, i.e. typing rules without premisses into
     * a constraint template.
     *
     * @param j - judgment declarations
     * @param cc - meta-variable classes that represent contexts
     */
    base(|j, cc) : TypingRule(RuleName(_, name), conclusion) ->
        <replace-meta-variables>
           Rule(name,
                <get-inputs(|j); remove-all(node-is-ctx(|cc))> conclusion,
                <get-outputs(|j)> conclusion,
                [])

    /**
     * Rule that transforms typing rules with premisses into a constraint template.
     *
     * FIXME: Allow checks for empty contexts
     *
     * @param j - judgment declarations
     * @param c - context declarartions
     * @param cc - meta-variable classes that represent contexts
     */
    step(|j, c, cc) : TypingRule(premisses-struct, RuleName(_, name), conclusion) ->
        <replace-meta-variables>
            Binding(<map(Fst)> vars-outputs,
                    <map(switch id
                         case (node-is-ctx-lookup, id):
                              (Trpl, map(Dupl; (id, \i -> <index(|i)> premisses\; Fst; get-outputs(|j))));
                              ((get-context-id, get-inputs(|c), get-outputs(|c)); mkLookup, id)
                         case where((get-generic-name; equal(|"Eq"), id)):
                              to-tuple; makeEq(|name)
                         case where((get-generic-name; equal(|"Neq"), id)):
                              to-tuple; makeNeq(|name)
                         otherwise: (get-inputs(|j), map(Dupl; (id, \i -> <index(|i)> sorted-premisses\; Fst; get-outputs(|j))));
                              (partition(not(node-is-ctx(|cc)), node-is-ctx(|cc)), id);
                              ((id, mapconcat(ctx-bind-to-kv-list(|c, cc))); mkJudgment, id)
                         end)> sorted-premisses,
                    // FIXME: Allow context-binds in conclusion, too.
                    Rule(name,
                         <get-inputs(|j); remove-all(node-is-ctx(|cc))> conclusion,
                         <get-outputs(|j)> conclusion,
                         <mapconcat(zip(makeEq(|name)))> vars-outputs))
        where
            premisses-list := <to-list(PremiseBase, PremiseCons)> premisses-struct;
            enum-premisses := <enum'> premisses-list;
            dependencies := <map((id, get-inputs(|j)); (id, compute-dependencies(|enum-premisses, j)))> enum-premisses;
            eval-order := <zip> (<map(Fst)> enum-premisses, <top-sort> dependencies);
            premisses := <zip> (premisses-list, <map(Snd)> dependencies);
            sorted-premisses := <map(\ (i, k) -> <index(|k); (id, replace(|i, k))> premisses\)> eval-order;
            vars-outputs := <map(Fst; (node-is-ctx-lookup < get-outputs(|c) + get-outputs(|j));
                                 Dupl; (length; newnames(|"X"); map(makeVar), id))> sorted-premisses

    /**
     * Replaces all meta-variables that occur within Output constructors
     * with fresh variables of the constraint language. This way the unification
     * has to deal only with one kind of variables.
     */
    replace-meta-variables : ast -> <collect-om(?Output(_); collect-om(node-is-meta-variable));
                                     flatten-list; Dupl; (id, length; newnames(|"X"); map(makeVar));
                                     zip; replace-all(|ast); topdown(try(\Output(n) -> n\))> ast

    /**
     * Transforms context bindings as they occur in the specification language into
     * a key-value list. This list contains triples with the context identifier, the inputs
     * and the outputs.
     *
     * @param c - context declarartions
     * @param cc - meta-variable classes that represent contexts
     */
    ctx-bind-to-kv-list(|c, cc) : node -> []
        where <node-is-ctx-empty <+ node-is-ctx-var(|cc)> node
    ctx-bind-to-kv-list(|c, cc) : node -> [(ctx, inputs, outputs) | <ctx-bind-to-kv-list(|c, cc)> tail]
        where <node-is-ctx-bind> node;
              ctx := <get-context-id> node;
              inputs := <get-inputs(|c)> node;
              outputs := <get-outputs(|c)> node;
              tail := <get-arguments; last> node
              
    /**
     * Returns the subset of the passed judgments that contain
     * at least one of the `inputs`.
     *
     * @param terms - judgments
     * @param decls - judgment declarations
     */
    compute-dependencies(|terms, decls) : inputs -> 
        <filter(where(Snd; get-outputs'(|decls);
               \ os -> <foldl(\ (i, l) -> <collect-om(?i); conc(|l)> os
                            \)> (inputs, [])
               \; not(equal([]))); Fst)> terms
    
strategies
    to-constraint-generator(|judgments, context-definitions, context-classes) =
         innermost(base(|judgments, context-classes) <+
                   step(|judgments, context-definitions, context-classes))