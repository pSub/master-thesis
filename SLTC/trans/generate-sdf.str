module generate-sdf

imports libstratego-lib
imports include/SLTC	
imports org/sugarj/languages/Sdf2

rules
	R : Module(_, Language(t), MetaVariables(MetaVariableList(Meta)), Contexts(Contexts(Ctx)), Judgments(JudgmentList(Judg)), _)
		-> 'module("Generated", imports(t), 
			exports(
				conc-grammars(context-free-syntax(Meta),
				conc-grammars(context-free-syntax(Ctx),
				context-free-syntax(Judg)))
			)
		)
		
	R : ContextDefinition(Name, X) -> [ 
		prod("\"()\"", Name, <cons> Name),
		prod(<concat-strings> [elem, " \";\" ", Name], Name,  <cons> Name),
		prod(<concat-strings> ["\"(\" ", Name, " \")\""], Name, <attr> "bracket")
	]
		where
			elem := <concat-strings><intersperse(|" \":\" ")> (X)
			
	R : MetaVariableDefinition(Name, MetaVariablePrefix(prefix), Scope(ids)) ->
		<map(\scope -> prod(<concat-strings>["\"", prefix, "\" [a-zA-Z]+"], scope, <cons> "Metavariable")\)> ids
				
	R : Judgment(None(), positions, None()) -> prod(<concat-strings><intersperse(|" ")> names, "TypingJudgment", <cons> "Judgment")
		where
			names := <map(proj <+ id)> positions
			
	proj : Hole(name, _) -> name

	cons : name -> <attr><concat-strings> ["cons(\"", name , "\")"]
	attr : str  -> <concat-strings> ["{", str, "}"]
				
    intersperse (|s) : [] -> []
    intersperse (|s) : [x] -> [x]
    intersperse (|s) : [x|y] -> [x,s| <intersperse(|s)> y]
	
strategies
	toSdf = innermost(R)