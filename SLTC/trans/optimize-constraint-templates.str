module optimize-constraint-templates

imports libstratego-gpp
imports libstratego-aterm

imports utils
imports constraint-language
imports constraint-template-language
imports constraint-utils
imports generate-fof
imports verification

// FIXME: This is only a temporary import for testing
imports generate-constraint-generator

signature
    sorts DummyVar
    constructors
        DummyVar__ : DummyVar

rules
    
    // FIXME: Currently there is no distinction between which and when ambiguities!
    solve-which-ambiguities(|context-definitions) : templates -> 
        <conc>(<map(mkFork; optimize-fork(|context-definitions, templates))> equality-classes,
               <diff(eq)> (templates,
                           <flatten-list> equality-classes))
        where equality-classes :=  <group-by-conclusion> templates
    
    /**
     * Collects all rules that have an equal pattern (modulo variable names).
     *
     * @type List(Rule) -> List(List(Rule))
     */
    group-by-conclusion : [] -> []
    group-by-conclusion : [r | rs] -> <if <equal(|[])> equality-class
                                       then <group-by-conclusion> rs
                                       else <MkCons> ([r | equality-class], <group-by-conclusion> rs)
                                       end>
        where equality-class := <filter(where(equal-mod-var(|r)))> rs
    
    /**
     * Extracts the pattern from the rules and compares the results modulo
     * variable names.
     *
     * @param a - Rule
     * @type Rule -> Rule
     */
    equal-mod-var(|a) = where(Dupl; (get-pattern, get-ctx-pattern); make-dummy-vars;
                              equal(|<Dupl; (get-pattern, get-ctx-pattern); make-dummy-vars> a))
    
    /**
     * Replaces all occurences of Var(_) with DummyVar(). This strategy
     * is used to implement equal-mod-var.
     * 
     * @internal
     * @type a -> a
     */
    make-dummy-vars = innermost(?Var__(_); !DummyVar__())

    /**
     * Stub for checking whether a rule within a Fork is redundant.
     */
    optimize-fork(|context-definitions, templates) : Fork__(rulez) -> Fork__(rulez'')
        where vars := <Hd; get-pattern; collect-om(?Var__(_))> rulez;
              // Make the conclusions of the rules equal.
              rulez' := [<Hd> rulez | <Tl; map(\rule -> <zip; replace-all(|rule)> (<get-pattern; collect-om(?Var__(_))> rule, vars)\)> rulez];
              rulez-premisses := <zip> (rulez', <map(\r -> <filter(not(?r); premisses-to-props)> rulez'\)> rulez');
              rulez'' := <remove-all(\(rule, premisses) ->
                                    <fetch-elem(
                                      \ps -> <premisses-to-props; add-indices;
                                         remove-all(
                                          \(i, p') ->
                                             <map(\p ->
                                                     Formula__(<get-rule-name; option-to-string-newname(|"Goal")> rule, Impl__(p', p))
                                                  \);
                                              fof-from-templates(|context-definitions, templates); verify-vampire(|"2", <conc-strings> (<int-to-string> i, <get-rule-name; option-to-string-newname(|"Goal")> rule)); some(?VerificationSuccess(_))> ps
                                          \); equal(|[])> rule
                                     \)> premisses
                                 \); map(Fst)> rulez-premisses

    optimize-templates(|context-definitions) = solve-which-ambiguities(|context-definitions)