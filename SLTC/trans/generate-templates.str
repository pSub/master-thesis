module generate-templates

imports constraint-language
imports constraint-template-language
imports sdf-utils
imports utils

imports org/sugarj/languages/Sdf2
imports include/SLTC
imports lib/editor-common.generated

imports optimize-constraint-templates

signature
    constructors
        UndefinedEq : term * List(term) -> term
       
rules
    
    wrapped-generate-templates = io-wrap(generate-templates)

    generate-templates :
        Module(ModuleDec(_),
               imports,
               Some(Language(target-language)),
               Contexts(context-definitions),
               MetaVariables(meta-variables),
               Judgments(judgments),
               Rules(typing-rules),
               conjectures)
        -> <to-templates(|judgments, context-definitions, target-language);
            optimize-templates(|target-language, context-definitions)> typing-rules
    generate-templates = failed

    /**
     * Rule that transforms a typing rule into a constraint template.
     *
     * @param j - judgment declarations
     * @param c - context declarartions
     */
    to-template(|j, c, l) : TypingRule(premisses-struct, RuleName(_, name), conclusion) ->
        <{|Fresh: replace-meta-variables|}>
          Template__(name,
                     <map(switch (get-typing-judgment, id)
                         case (node-is-ctx-lookup, id):
                              \(lookup, deps) ->
                                (Lookup__(<get-typing-judgment; get-context-id> lookup,
                                          <get-typing-judgment; get-inputs(|c)> lookup,
                                          <get-typing-judgment; get-outputs(|c)> lookup,
                                          <get-error-message> lookup)
                                 , <map(Dupl; (id, \i -> <index(|i)> sorted-premisses\; Fst; get-typing-judgment; get-outputs(|j')))> deps)\
                         case where((node-is-generic(|"Eq"), id)):
                              \(judg, dep) -> (<get-typing-judgment; to-tuple;
                                                makeEq(|<get-error-message; option(filter(?Error(_)))> judg, <get-typing-judgment; get-judgment-id> judg)> judg, dep) \
                         case where((node-is-generic(|"Neq"), id)):
                              \(judg, dep) -> (<get-typing-judgment; to-tuple;
                                                makeNeq(|<get-error-message> judg, <get-typing-judgment; get-judgment-id> judg)> judg, dep) \
                         case where((node-is-generic(|"TypingJudgment"), id)):
                              \(judgment, deps) ->
                                 (Judgment__(<get-typing-judgment; get-judgment-id> judgment,
                                            <get-typing-judgment; get-inputs(|j'); filter(not(node-is-ctx))> judgment,
                                            <get-typing-judgment; get-inputs(|j'); filter(node-is-ctx); mapconcat(ctx-bind-to-kv-list(|c))> judgment,
                                            <get-typing-judgment; get-outputs(|j')> judgment,
                                            <get-error-message> judgment)
                                 , <map(Dupl; (id, \i -> <index(|i)> sorted-premisses\; Fst; get-typing-judgment; get-outputs(|j')))> deps) \
                         otherwise: exit-with-error(|1, "Unkown constructor encountered.")
                         end)> sorted-premisses,
                     Conclusion__(
                         <get-typing-judgment; get-judgment-id> conclusion,
                         <get-typing-judgment; get-inputs(|j'); partition(not(node-is-ctx), node-is-ctx);
                         (id, mapconcat(ctx-bind-to-kv-list(|c)))> conclusion',
                         <get-typing-judgment; get-outputs(|j')> conclusion'))
        where
            // Convert premisses into a list.
            premisses-list := <to-list(PremiseBase, PremiseCons)> premisses-struct;

            judgment-types := <get-judgment-types(!"I" | <get-typing-judgment; get-judgment-id> conclusion)> j;

            // Transform implicit equalities in the conclusion into explicit equalities. Implicit
            // equalities are removed by introducing fresh variables and equality constraints
            // between them. This results in a list of variable substitutions and a list of premisses.
            (substitutions, (equalities, judgment-definitions)) :=
                    <get-typing-judgment; get-inputs(|j);
                     make-implicit-equalities-explicit(true | <get-error-message> conclusion, conclusion, judgment-types, l)> conclusion;

            // Replace each occurence with the corresponding new meta-variable.
            conclusion' := <concat; replace-once(|conclusion)> substitutions;

            // Replace every occurence with only one representative of the new meta-variables.
            premisses-list' := <map(Hd); replace-all(meta-variable-equal|premisses-list)> substitutions;

            // Transform implicit equalities in the premisses into explicit equalities.
            premisses-impl-eq := <map(\premise -> <get-typing-judgment; (node-is-ctx-lookup < get-inputs(|c) + get-inputs(|j));
                                      make-implicit-equalities-explicit(false | <get-error-message> premise, conclusion', <get-judgment-types(!"I" | <get-typing-judgment; get-judgment-id> premise)> j, l)> premise\)> premisses-list';

            // Replace every occurence with only one representative of the new meta-variables.
            premisses-list'' := <zip; map(\(premise, subst) -> <concat; replace-once(|premise)> subst\)> (premisses-list', <map(Fst)> premisses-impl-eq);

            // Add the explicit equalities to the list of premisses.
            premisses-with-equalities := <conc> (premisses-list'', equalities, <mapconcat(Snd; Fst)> premisses-impl-eq);
            
            // Determine the number of judgments to generate names for the newly created judgments
            previous-judgments := <CollectedJudgments>;
            number-of-judgments := <length> previous-judgments;
            
            // Collect all new judgment definitions
            new-judgment-defs := <conc> (judgment-definitions, <mapconcat(Snd; Snd)> premisses-impl-eq);
            
            // Create substitutions for the tentative equality judgments
            eq-judg-subst := <collect(?UndefinedEq(<id>,_)); map-with-index(\(i, j) -> (j, <add; int-to-string> (i, number-of-judgments))\)> premisses-with-equalities;
            
            // Replace tentative equality judgments with real equality judgments
            premisses-with-equalities' := <replace-all(|premisses-with-equalities);
                                           innermost(\UndefinedEq(n, args) ->
                                                        <mkterm> (<conc-strings> ("Eq-", n),
                                                                  args)\)> eq-judg-subst;
            
            // Add new judgment definitions for the implicit equaltities
            j' := <conc> (j, judgment-definitions, new-judgment-defs);

            // Collect all newly generated judgments and propagate them outwards using dynamic rules
            rules(CollectedJudgments :+ _ -> <conc> (previous-judgments, judgment-definitions, new-judgment-defs));

            // Enumerate the premisses.
            enum-premisses := <add-indices> premisses-with-equalities';

            // Compute the dependencies between the premisses.
            dependencies := <map((id, get-typing-judgment; (node-is-ctx-lookup < get-inputs(|c) + get-inputs(|j')));
                                 (id, compute-dependencies(|enum-premisses, conclusion', j', c)))> enum-premisses;

            // Create a topological order of the premisses according to their dependencies.
            eval-order := <zip> (<map(Fst)> enum-premisses, <top-sort <+ exit-with-error(|<conc-strings> ("Cyclic dependencies in rule ", <option-to-string> name), 1)> dependencies);
            
            // Assign the dependencies to the premisses.
            premisses := <zip> (premisses-with-equalities', <map(Snd)> dependencies);
            
            // Sort the premisses according to the topological order. This includes updating references
            // to premisses in the dependencies.
            sorted-premisses := <map(\ (i, k) -> <index(|k); (id, replace(|i, k))> premisses\)> eval-order

    /**
     * Replaces all meta-variables with fresh variables of the constraint language.
     * This way the unification has to deal only with one kind of variables.
     */
    replace-meta-variables : ast -> <collect-om(node-is-meta-variable); Dupl;
                                     (id, length; newnames(|"X"); map(makeVar));
                                     zip; replace-all(|ast)> ast
    replace-meta-variables = failed

    /**
     * Transforms context bindings as they occur in the specification language into
     * a key-value list. This list contains triples with the context identifier, the inputs
     * and the outputs.
     *
     * @param c - context declarartions
     */
    ctx-bind-to-kv-list(|c) : node -> [Ctx__(ctx)]
        where <node-is-ctx-var> node;
              ctx := <get-context-id> node
    ctx-bind-to-kv-list(|c) : node -> [Reset__(ctx)]
        where <node-is-ctx-empty> node;
              ctx := <get-context-id> node
    ctx-bind-to-kv-list(|c) : node -> [Binding__(ctx, inputs, outputs) | <ctx-bind-to-kv-list(|c)> tail]
        where <node-is-ctx-bind> node;
              ctx := <get-context-id> node;
              inputs := <get-inputs(|c)> node;
              outputs := <get-outputs(|c)> node;
              tail := <get-arguments; last> node
    ctx-bind-to-kv-list(|c) = failed(|[c])
              
    /**
     * Returns the subset of the passed judgments that contain
     * at least one of the `inputs`.
     *
     * @param terms - judgments
     * @param decls - judgment declarations
     */
    compute-dependencies(|terms, conclusion, judgments, contexts) : inputs ->
        <filter(where(Snd; get-typing-judgment; (node-is-ctx-lookup < get-outputs(|contexts) + get-outputs(|judgments));
                \ os -> <foldl(\ (i, l) -> <collect-om(?i); conc(|l)> os
                               \)> (input-meta-variables, [])
                \; diff(|conclusion-meta-variables); not(equal([]))); Fst)> terms
        where input-meta-variables := <collect-om(node-is-meta-variable)> inputs;
              conclusion-meta-variables := <get-typing-judgment; get-inputs(|judgments); collect-om(node-is-meta-variable)> conclusion
    compute-dependencies(|terms, conclusion, judgments, contexts) =
        failed(|[terms, conclusion, judgments, contexts])

    /**
     * Generates from implicit equalities equality premisses.
     */
    make-implicit-equalities-explicit(is-conclusion | errors, conclusion, types, language) =
        ?inputs; <zip> (inputs, types);
        mapconcat(where(Fst; resolvable(is-conclusion | conclusion))
                  < singelton 
                  + (Fst; collect-meta-variables(is-conclusion | conclusion, language)));
        flatten-list;
        group((Fst, Fst); meta-variable-equal); filter(where(length; gt(|1)));
        Dupl; (id, map(map(make-new-meta-variable))); zip;
        Dupl; (map((map(Fst), id); zip), mapconcat(make-eq-judgments(|errors)); unzip)
    make-implicit-equalities-explicit(is-conclusion | errors, conclusion, types, language) =
        failed(|[errors, conclusion, types, language])
        
    collect-meta-variables(is-conclusion | conclusion, language) : v -> []
        where <or(is-string, get-arguments; equal(|[]))> v
    collect-meta-variables(is-conclusion | conclusion, language) : cons#(args) ->
        <conc> (<compute-types(map(where(Snd; length; equal(|1))
                                 < (id, \[sort(type)] -> type\)
                                 + exit-with-error(|999, "not implemented")) | language, cons, arg-count)> vars,
                <map(collect-meta-variables(is-conclusion | conclusion, language))> nodes)
        where arg-count := <length> args;
              (vars, nodes) := <partition(resolvable(is-conclusion | conclusion),
                                          not(resolvable(is-conclusion | conclusion)))> args
    collect-meta-variables(is-conclusion | conclusion, language) =
        failed(|[conclusion, language])

    /**
     * Computes the types of the passed meta-variables according to the surrounding
     * constructor and the scope of the meta-variable.
     *
     * @param target language
     * @param constructor that surrounds the variables
     * @param number of arguments of the constructor
     * @type List(MetaVariable) -> List(String)
     */
    compute-types(disambiguate | language, cons, arg-count) : [] -> []
    compute-types(disambiguate | language, cons, arg-count) : vars -> vars-types
        where
              productions := <get-productions-for-constructor(|language)> cons;
              productions' := <filter(to-tuple; Fst; filter(?sort(_)))> productions;
              matching-productions := <filter(where(length; equal(|arg-count)))>;
              transposed-matching-productions := <matrix-transpose> matching-productions;
              vars-types := <zip; disambiguate> (vars, transposed-matching-productions)
    compute-types(|language, cons, arg-count) = failed(|[language, cons, arg-count])
              
    /**
     * A node is resolvable (in the context of implicit equalities) if it is a meta-variable
     * and it either is in the conclusion or if it is in a premise it must not occur in the conclusion.
     *
     * @param succeeds if the passed node is in the conclusion, fails otherwise
     * @param conclusion of the typing rule
     */
    resolvable(is-conclusion | conclusion) =
        and(node-is-meta-variable, is-conclusion <+ \n -> <collect(?n); equal(|[])> conclusion\)

    /**
     * Creates a new meta-variable of the same class.
     */
    make-new-meta-variable : (meta-variable, type) -> <mkterm> (<get-constructor> meta-variable,
                                                                [<newname> "X", None()])
    make-new-meta-variable = failed

    /**
     * Creates from a given list of meta-variables a list of equality judgments.
     */
    make-eq-judgments(|errors) : (old, new) -> <zip; make-eq-judgments'(|relevant-errors)> (new, types)
        where (_, types) := <unzip> old;
              relevant-errors := <option(filter(where(\error ->
                                                        <subseteq> (<collect-om(node-is-meta-variable)> error,
                                                                  old)
                                                      \);
                                                \Implicit(error) ->
                                                  Error(<zip; replace-all(|error)> (old, new))
                                                \))> errors
    make-eq-judgments(|errors) = failed(|[errors])
    
    make-eq-judgments'(|errors) : [] -> []
    make-eq-judgments'(|errors) : [(x, type)] -> []
    make-eq-judgments'(|errors) : [(x, t), (y, s) | xs] ->
        [(eq-judgment, judgment-definition) | <make-eq-judgments'(|errors)> [y | xs]]
        where eq-judgment := ErrorTypingJudgment(UndefinedEq(<make-fresh> "J", [x, y]), errors);
              judgment-definition := Judgment(None(), [Hole(t, "I"), Hole(s, "I")], None(), Some(BuiltIn("Eq")))
    make-eq-judgments'(|errors) = failed(|[errors])
    
strategies
    to-templates(|judgments, context-definitions, target-language) =
         ?typing-rules; {|CollectedJudgments: rules(CollectedJudgments :+ _ -> judgments);
         !(<innermost(to-template(|judgments, context-definitions, target-language))> typing-rules,
           <CollectedJudgments>) |}
    to-templates(|judgments, context-definitions, target-language) =
        failed(|[judgments, context-definitions, target-language])