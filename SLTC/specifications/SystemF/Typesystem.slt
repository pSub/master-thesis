module Typesystem

language SystemF

meta-variables 	Term "~" { Type Exp }
				Ctx "$" { TermBinding TypeBinding }
				Id "%" { ID }
                Num "&" { Int }

contexts
TermBinding := ID{I} x Type{O}
TypeBinding := ID{I}

judgments
TermBinding{I} "|" TypeBinding{I} "|-" Exp{I} ":" Type{O}.
Type{O} "= [" ID{I} "->" Type{I} "]" Type{I}.
ID{I} "fresh in" TypeBinding{I}.
ID{I} "!=" ID{I} is Neq.

rules

/* Typing rules */

%x : ~T in $C1        @error %x "should have type" ~T "but has type" INFTYPE.
=============== T-Var
$C1 | $C2 |- %x : ~T

============== T-int
$C1 | $C2 |- &i : int

(%x : ~T1 ; $C1) | $C2 |- ~t2 : ~T2     @error ~t2 "should have type" ~T2 "but has type" INFTYPE.
==================================== T-Abs
$C1 | $C2 |- \ %x : ~T1 . ~t2 : ~T1 -> ~T2

$C1 | $C2 |- ~t1 : ~T11 -> ~T12     @error ~t1 "should have type" ~T11 "->" ~T12 "but has type" INFTYPE.
$C1 | $C2 |- ~t2 : ~T11             @error ~t2 "should have type" ~T11 "but has type" INFTYPE.
================================ T-App
$C1 | $C2 |- ~t1 ~t2 : ~T12

$C1 | (%x ; $C2) |- ~t2 : ~T2           @error ~t2 "should have type" ~T2 "but has type" INFTYPE.
%x fresh in $C2
======================================= T-Tabs
$C1 | $C2 |- \ %x . ~t2 : all %x . ~T2

~U = [ %x -> ~S ] ~T            @error ~U "is not" ~T "where" %x "is replaced by" ~S.
$C1 | $C2 |- ~e : all %x . ~T   @error ~e "should have type all" %x "." ~T "but has type" INFTYPE.
============================== T-Tapp
$C1 | $C2 |- ~e [ ~S ] : ~U


/* Type substitution */

/**
 * The following two rules are not syntax-directed.
 */
===================== Subst-Eq
~S = [ %x -> ~S ] %x

%y != %x
==================== Subst-Neq
%y = [ %x -> ~S ] %y

~U = [ %x -> ~S ] ~T
========================================== Subst-All
(all %y . ~U) = [ %x -> ~S ] (all %y . ~T)

======================
int = [ %x -> ~S ] int

~U1 = [ %x -> ~S ] ~T1
~U2 = [ %x -> ~S ] ~T2
==================================== Subst-Arrow
~U1 -> ~U2 = [ %x -> ~S ] ~T1 -> ~T2

/* Freshness of type variables */

==============
%x fresh in ()

%x != %y
%x fresh in $C
=====================
%x fresh in (%y ; $C)

conjectures

/* Positive conjectures */

===
x fresh in (a; b; c; ())

===
y = [ x -> int ] y

===
int = [ x -> int ] x

===
() | () |- \ x . \ y : x . y : all x . (x -> x)

===
() | () |- \ x . \ f : x -> x . \ a : x . (f (f a)) : all x . ((x -> x) -> (x -> x))

===
() | () |- (\ x . \ y : x . y) [ int ] : int -> int

===
() | () |- (\ x . \ f : x -> x . \ a : x . (f (f a))) [ int ] : (int -> int) -> (int -> int)

===
() | () |- (\ x . \ f : x -> x . \ a : x . (f (f a))) [ int -> int ] : ((int -> int) -> (int -> int)) -> ((int -> int)-> (int -> int))

===
() | (x ; ()) |- \ y . (\ f : x . f) : all y . (x -> x)

===
() | () |- (\ x . \ y . \ z : x . z) : all x . all y . (x -> x)

===
() | () |- (\ x . \ y . \ z : y . z) : all x . all y . (y -> y)

===
() | () |- (\ x . \ y . \ z . \ a : y . a) : all x . all y . all z . (y -> y)

===
() | () |- (\ x . \ y . \ z : x . z) [ int ] : all y . (int -> int) 

/* Negative Conjectures */


===
x fresh in (y; x; ())

===
int = [ x -> int ] y

===
() | () |- \ x : int . x : int

===
() | () |- (\x : int . x) x : int

===
() | () |- 1 1 : int

======
f : int ; () | () |- f 1 : int

======
f : (int -> int); () | () |- f f : int

===
() | () |- x : int

===
() | () |- \ x . \ y : x . y : all x . x

===
() | () |- (\ x . \ y : y . y) [ int ] : int -> int

===
() | () |- (\ x . \ y . \ x . \ a : y . a) : all x . all y . all z . (y -> y)
